buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'groovy'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
    id 'java-gradle-plugin'
    id 'com.github.ben-manes.versions' version '0.27.0'
}

apply from: 'mvn-push.gradle'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation gradleApi()
    implementation 'org.codehaus.groovy:groovy:2.5.9'
    implementation 'org.marketcetera.fork:commons-csv:3.2.0'
    implementation 'org.apache.poi:poi-ooxml-schemas:4.1.1'
    implementation 'org.apache.poi:poi-ooxml:4.1.1'
    implementation 'org.apache.poi:poi:4.1.1'
    implementation 'org.apache.poi:ooxml-schemas:1.4'
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.assertj:assertj-core:3.15.0'
    testImplementation 'org.xmlunit:xmlunit-core:2.6.3'
}

group = GROUP
version = VERSION_NAME

pluginBundle {
    website = POM_URL
    vcsUrl = POM_SCM_URL
    description = POM_DESCRIPTION
    tags = ['localization', 'android']

    plugins {
        localization {
            displayName = POM_NAME
            description = POM_DESCRIPTION
        }
    }
}

gradlePlugin {
    plugins {
        localization {
            id = 'pl.droidsonroids.localization'
            implementationClass = 'pl.droidsonroids.gradle.localization.LocalizationPlugin'
        }
    }
}

jacoco {
    toolVersion = '0.8.4'
}

jacocoTestReport {
    reports {
        xml.required.set(true)
        csv.required.set(false)
    }
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.register('copyTestResources', Copy) {
    from "${projectDir}/src/test/resources"
    into "${buildDir}/classes/test"
    group = 'verification'
    description = 'To be used in Android Studio, workaround for https://code.google.com/p/android/issues/detail?id=64887'
}

wrapper {
    gradleVersion = '8.7'
    distributionType = Wrapper.DistributionType.BIN
}